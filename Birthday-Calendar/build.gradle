apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:color:3.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.work:work-runtime-ktx:2.11.0"
}

android {
    compileSdk = 35

    defaultConfig {
        minSdkVersion = 28
        targetSdkVersion = 34
        versionCode = 20
        versionName = "2.1.0"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }


    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"

        signingConfigs {
            release {
                storeFile = file(signingStoreLocation)
                storePassword = signingStorePassword
                keyAlias = signingKeyAlias
                keyPassword = keyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    flavorDimensions += "version"
    productFlavors {
        create("full") {
            dimension = "version"
            applicationId = "org.birthdayadapter"
            buildConfigField("boolean", "FULL_VERSION", "true")
            buildConfigField("boolean", "GOOGLE_PLAY_VERSION", "false")
        }
        create("full_google") {
            dimension = "version"
            applicationId = "org.birthdayadapter"
            buildConfigField("boolean", "FULL_VERSION", "true")
            buildConfigField("boolean", "GOOGLE_PLAY_VERSION", "true")
        }
        create("free") {
            dimension = "version"
            applicationId = "org.birthdayadapter.free"
            buildConfigField("boolean", "FULL_VERSION", "false")
            buildConfigField("boolean", "GOOGLE_PLAY_VERSION", "true")
        }
    }
    namespace = 'org.birthdayadapter'
    lint {
        disable('MissingTranslation', 'ExtraTranslation')
    }
    buildFeatures {
        buildConfig = true
    }
}
