apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.work:work-runtime-ktx:2.11.0"
}

android {
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 36
        versionCode = 36
        versionName = "3.6"

        def gitCommitHashProvider = providers.exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
        }.standardOutput.asText.map { it.trim() }
        buildConfigField "String", "GIT_COMMIT_HASH", gitCommitHashProvider.map{ commit -> "\"${commit}\"" }.get()
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
        }
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"

        signingConfigs {
            release {
                storeFile = file(signingStoreLocation)
                storePassword = signingStorePassword
                keyAlias = signingKeyAlias
                keyPassword = signingKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    flavorDimensions += "version"
    productFlavors {
        create("full") {
            dimension = "version"
            applicationId = "org.birthdayadapter"
            buildConfigField("boolean", "FULL_VERSION", "true")
            buildConfigField("boolean", "GOOGLE_PLAY_VERSION", "false")
            manifestPlaceholders = [authority: "org.birthdayadapter"]
            resValue "string", "account_type", "org.birthdayadapter.auth"
        }
        create("full_google") {
            dimension = "version"
            applicationId = "org.birthdayadapter"
            buildConfigField("boolean", "FULL_VERSION", "true")
            buildConfigField("boolean", "GOOGLE_PLAY_VERSION", "true")
            manifestPlaceholders = [authority: "org.birthdayadapter"]
            resValue "string", "account_type", "org.birthdayadapter.auth"
        }
        create("free") {
            dimension = "version"
            applicationId = "org.birthdayadapter.free"
            buildConfigField("boolean", "FULL_VERSION", "false")
            buildConfigField("boolean", "GOOGLE_PLAY_VERSION", "true")
            manifestPlaceholders = [authority: "org.birthdayadapter.free"]
            resValue "string", "account_type", "org.birthdayadapter.free.auth"
        }
    }
    namespace = 'org.birthdayadapter'
    lint {
        disable('MissingTranslation', 'ExtraTranslation')
    }
    buildFeatures {
        buildConfig = true
    }
}
